use crate::ast::*;


// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  // 剩下的情况采用默认方式处理
  _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { func_def:Box::new(func_def) };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { func_type, ident, block:Box::new(block) }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <stmt: Stmt> "}" => Block { stmt:Box::new(<>) };

Stmt: Stmt = "return" <exp: Exp> ";" => Stmt { exp:Box::new(<>) };

Exp: Exp = <add_exp: AddExp> => Exp { add_exp:Box::new(<>) };

AddExp: AddExp = {
    MulExp                                  => AddExp::MulExp(Box::new(<>)),
    <lhs: AddExp> <op: AddOp> <rhs: MulExp> => AddExp::Binary(Box::new(lhs), op, Box::new(rhs)),
};

MulExp: MulExp = {
    UnaryExp                                  => MulExp::Unary(Box::new(<>)),
    <lhs: MulExp> <op: MulOp> <rhs: UnaryExp> => MulExp::Binary(Box::new(lhs), op, Box::new(rhs)),
};

AddOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Minus,
};

MulOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
    "%" => BinOp::Mod,
};

PrimaryExp: PrimaryExp = {
    "(" <e:Exp> ")" => PrimaryExp::Paren(Box::new(e)),
    <n:Number>      => PrimaryExp::Number(n),
};

UnaryExp: UnaryExp = {
    <p:PrimaryExp>            => UnaryExp::Primary(Box::new(<>)),
    <op:UnaryOp> <e:UnaryExp> => UnaryExp::UnaryOp(op, Box::new(e)),
};

UnaryOp: UnaryOp = {
    "+" => UnaryOp::Plus,
    "-" => UnaryOp::Minus,
    "!" => UnaryOp::Not,
};

Number: i32 = <num: IntConst> => <>;

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}